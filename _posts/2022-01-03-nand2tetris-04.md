---
layout: post
title: コンピュータシステムの理論と実装 (4)
date: 2022-01-03
categories: [Technology, Study]
tags: [Low-Level, Assembly, Hack, nand2tetris]
---

## 4 章 機械語

- 本章では Hack 機械語 というアセンブリ言語を書いていく。`A` がそのとき指しているアドレスの値、`M` がアドレス `A` が指しているメモリワードの値、`D` は値を格納できる変数のようなものと考えるとわかりやすい。
- 基本的に 1 や 0 以外の定数をそのまま計算に使うことはできないので、例えば定数を `D` に代入したい場合は

```hack
@5
D = M
```

などとするしかない。

### 課題

- 下記 `.asm` コードを動かすためには、いったん `Assembler.bat` で `.hack` ファイルを生成したうえで、それを `CPUEmulator.bat` で検証する必要があるので注意。`.asm` コードを編集した場合その都度バイナリに変換する必要がある。

- `Memory[R0]` および `Memory[R1]` に現在格納されている値を計算して、`Memory[R2]` に格納するプログラムを書いていく。ただの乗算であるが当然 `*` なんて使えるはずがないので、小学生のころの気持ちに戻ってループを回し加算を繰り返す形になる。

{: file="Mult.asm"}

```
    @i
    M = 1 // Memory[i] = 1
    @R2
    M = 0 // Memory[R2] = 0
(LOOP)
    @i
    D = M // D = Memory[i]
    @R1
    D = D - M // D = Memory[i] - Memory[R1]
    @END // ここでジャンプ先ラベルを指定する (実行コードの特定の行数を指す定数でもよい)
    D;JGT // if D > 0, jump to END
    @R0
    D = M // D = Memory[R0]
    @R2
    M = D + M // Memory[R2] += D (Memory[R0])
    @i
    M = M + 1 // Memory[i] = Memory[i] + 1
    @LOOP
    0;JMP
(END)
    @END
    0;JMP
```

- 次の課題は、まずスクリーンの全面を黒く描画したうえで、押されていない場合では真っ白に塗りつぶすというものである。スクリーンのピクセルは 512 x 256 であり、ワードが 16 bit であることを踏まえるとここでは $(512/16) \times 256 = 8192$ ワードが存在することになる。これに対して順番にループを回し画面の描画を行っていく。
- このとき、1 ワード全体を塗りつぶすには当該レジスタに `1111111111111111` を代入する必要があるので注意。`1` を代入すると最も左のピクセルのみ塗りつぶされることになってしまう。上記を符号付き 2 進数とみなすと 10 進数では -1 となるので、`M = -1` とすれば OK。  

{: file="Fill.asm"}

```
(LOOP)
    @24576 // 押されているキーの ASCII コードが現れる場所
    // 何も押されていない場合は 0 が現れる
    D = M // D = Memory[24576]
    @WHITE // キーが押されている場合は (WHITE) に移動
    D;JGT // if D > 0, jump to WHITE
(BLACK)
    @i
    M = 0 // Memory[i] = 0
(BLACKLOOP)
    @i
    D = M // D = Memory[i]
    @8191
    D = D - A // D = D - 8191
    @BLACKEND
    D;JGT // if D > 0, jump to END
    @i
    D = M // D = Memory[i]
    @SCREEN
    A = A + D // D = SCREEN + Memory[i]
    M = -1 // paint to black
    @i
    M = M + 1 // Memory[i] += 1
    @BLACKLOOP
    0;JMP
(BLACKEND)
    @LOOP
    0;JMP // 無限ループ
(WHITE)
    @j
    M = 0
(WHITELOOP)
    @j
    D = M // D = Memory[j]
    @8191
    D = D - A // D = D - 8191
    @WHITEEND
    D;JGT // if D > 0, jump to END
    @j
    D = M // D = Memory[j]
    @SCREEN
    A = A + D // A = SCREEN + Memory[j]
    M = 0 // paint to white
    @j
    M = M + 1 // j = j + 1
    @WHITELOOP
    0;JMP
(WHITEEND)
    @LOOP
    0;JMP
```
